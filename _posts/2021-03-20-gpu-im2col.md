---
layout: post
title:  "CUDA im2col 實作"
---

繼續[上篇](https://jysh1214.github.io/2021/03/10/cpu-im2col.html)實作`cpu`版的`im2col`。
這次來實作`CUDA`版本的`im2col`。

`im2col.cu`
```c++=
__global__ void im2col_kernel(cv::cuda::PtrStepSz<float1> cu_padded,
    int k_rows,
    int k_cols,
    int stride,
    cv::cuda::PtrStepSz<float1> cu_dst)
{
    unsigned int id_x = blockIdx.x * blockDim.x + threadIdx.x;
    unsigned int id_y = blockIdx.y * blockDim.y + threadIdx.y;

    int k_rows_radius = k_rows / 2;
    int k_cols_radius = k_cols / 2;

    unsigned int i = k_rows_radius + id_x * stride;
    unsigned int j = k_cols_radius + id_y * stride;

    int rows_limit = (k_rows % 2 == 0)? (cu_padded.rows - k_rows_radius + 1): (cu_padded.rows - k_rows_radius);
    int cols_limit = (k_cols % 2 == 0)? (cu_padded.cols - k_cols_radius + 1): (cu_padded.cols - k_cols_radius);

    if (i < rows_limit && j < cols_limit) {
        unsigned int current_row = id_x * ((cols_limit - k_cols_radius) / stride) + id_y;
        for (int r = 0; r < k_rows; r++) {
            for (int c = 0; c < k_cols; c++) {
                int src_row = i - k_rows_radius + r;
                int src_col = j - k_cols_radius + c;
                cu_dst(current_row, r * k_cols + c) = cu_padded(src_row, src_col);
            }
        }
    }
}

void gpu_im2col(const cv::Mat& src, const int k_rows, const int k_cols, const int padding, const int stride, cv::Mat& dst)
{
    assert(src.rows >= k_rows && src.cols >= k_cols);
    assert(padding >= 0 && stride >= 1);

    int conv_rows = (src.rows + 2 * padding - k_rows) / stride + 1;
    int conv_cols = (src.cols + 2 * padding - k_cols) / stride + 1;

    cv::Mat padded = cv::Mat::zeros(src.rows + 2 * padding, src.cols + 2 * padding, CV_32F);
    src.copyTo(padded(cv::Rect(padding, padding, src.cols, src.rows)));

    cv::cuda::GpuMat cu_padded;
    cu_padded.upload(padded);

    int blockSize = 8;

    int grid_x = (conv_rows + blockSize - 1) / blockSize; // src rows
    int grid_y = (conv_cols + blockSize - 1) / blockSize; // src cols

    dim3 grid(grid_x, grid_y, 1);
    dim3 block(blockSize, blockSize, 1);

    cv::cuda::GpuMat cu_dst;
    dst = cv::Mat(conv_rows * conv_cols, k_rows * k_cols, CV_32F, cv::Scalar(0.0));
    cu_dst.upload(dst);

    im2col_kernel<<<grid, block>>>(cu_padded, k_rows, k_cols, stride, cu_dst);

    if (cudaSuccess != cudaGetLastError()) {
        std::cout << "kernel fault.\n";
    }

    cu_dst.download(dst);
}
```

搭配`cuda::gemm`實現`CUDA`的卷積功能。

```c++=
void gpu_conv(const cv::Mat& src, const cv::Mat& kernel, const int padding, const int stride, cv::Mat& dst)
{
    assert(kernel.rows <= src.rows && kernel.cols <= src.cols);

    int k_rows = kernel.rows;
    int k_cols = kernel.cols;

    cv::Mat reshape_src;
    gpu_im2col(src, k_rows, k_cols, padding, stride, reshape_src);

    cv::Mat reshape_ker = kernel.reshape(kernel.channels(), kernel.rows * kernel.cols).clone();
    cv::Mat empty, gemm_result;
    cv::cuda::gemm(reshape_src, reshape_ker, 1.0, empty, 0.0, gemm_result);

    int dst_rows = (src.rows + 2 * padding - k_rows) / stride + 1;
    dst = gemm_result.reshape(gemm_result.channels(), dst_rows).clone();
}
```

## 與`OpenCV`內建的`Convolution`函式比較

使用
$$
\begin{bmatrix}
 -1 & -1 & -1\\
 -1 &  8 & -1\\
 -1 & -1 & -1
\end{bmatrix}
$$
為`kernel`。

原始圖片
<center><img src="/assets/images/test.jpg" width="650"></center>

1. 使用`cv::cuda::Convolution`

    ```c++=
    cv::Mat src, filter, dst;
    cv::cuda::GpuMat cu_src, cu_gray, cu_filter, cu_dst;

    int filter_height = 3;
    int filter_width = 3;

    float filter_data[filter_height][filter_width] = {
        {-1, -1, -1},
        {-1,  8, -1},
        {-1, -1, -1}
    };

    filter = cv::Mat(filter_height, filter_width, CV_32FC1, filter_data);
    cu_filter.upload(filter);

    cv::Ptr<cv::cuda::Convolution> conv = cv::cuda::createConvolution(cv::Size(filter_height, filter_width));

    src = cv::imread("test.jpg");
    cu_src.upload(src);
    cv::cuda::cvtColor(cu_src, cu_gray, cv::COLOR_BGR2GRAY);

    // cuda convolve: Only CV_32FC1 images are supported for now.
    cu_gray.convertTo(cu_gray, CV_32FC1);

    conv->convolve(cu_gray, cu_filter, cu_dst);
    cu_dst.download(dst);

    dst.convertTo(dst, CV_8UC1);
    cv::imwrite("cuda_output.png", dst);
    ```

    <center><img src="/assets/images/2021-03-20-gpu-im2col/gpu_output.png" width="650"></center>

2. 使用上面的`gpu_conv`

    <center><img src="/assets/images/2021-03-20-gpu-im2col/my_conv_output.png" width="650"></center>

3. 使用`CPU`版本的`filter2D`

    ```c++=
    cv::filter2D(gray, dst, -1, filter, cv::Point(-1, -1), 0, cv::BORDER_CONSTANT);
    ```

    <center><img src="/assets/images/2021-03-20-gpu-im2col/cpu_output.png" width="650"></center>

可以看出，`cv::cuda::Convolution`將圖片分割成數塊同時做卷積，
而`gpu_im2col`效果較接近`CPU`版的`filter2D`。

## Reference

1. [caffe im2col](https://github.com/BVLC/caffe/blob/master/src/caffe/util/im2col.cu)
